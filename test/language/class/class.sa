
class Vec2
  function _init(x, y)
    this.x = x; this.y = y
  end
  function _str
    return "<${this.x}, ${this.y}>"
  end
  function +(other)
    return Vec2(this.x + other.x,
                this.y + other.y)
  end
  function +=(other)
    this.x += other.x
    this.y += other.y
    return this
  end
  function ==(other)
    return this.x == other.x and this.y == other.y
  end
end

v1 = Vec2(1, 2); assert(v1.x == 1 and v1.y == 2)
print("v1 = $v1")

assert(str(v1) == "<1, 2>")

v2 = Vec2(3, 4); assert(v2.x == 3 and v2.y == 4)
print("v2 = $v2")

v3 = v1 + v2
print("v3 = $v3")

assert(v1 == Vec2(1, 2))

v1 += v2
assert(v1 == v3)

class Path
  function _init(path)
    this.path = path
  end
  function /(other)
    if other is String
      return Path(this.path + "/" + other)
    elif other is Path
      return Path(this.path + "/" + other.path)
    else
      assert(false, "Invalid type")
    end
  end
  function _str
    return this.path
  end
end

local = Path("/usr/local")
saynaa = Path("bin/saynaa")
print('local/saynaa = "${local/saynaa}"')
assert(str(local/saynaa) == "/usr/local/bin/saynaa")
assert(str(local/"lib") == "/usr/local/lib")

class N
  function _init(val)
    this.val = val
  end
  function _str()
    return "N(${this.val})"
  end
  function +(other)
    return N(this.val + other.val)
  end
  function +=(other)
    this.val += other.val
    return this
  end
  function %=(num)
    this.val %= num
    return this
  end
  function ==(other)
    return this.val == other.val
  end
  function <<(num)
    this.val *= 10
    this.val += num
    return this
  end
  function >>(other)
    other.val = this.val % 10
    this.val = (this.val - other.val) / 10
  end
  
  function !this()
    this.val -= 1
    return this.val + 1
  end
end

n1 = N(12)
n2 = N(23)
assert(n1 + n2 == N(n1.val + n2.val))
n1 %= 5
assert(n1.val == 2)

n3 = N(4) << 8 << 3 << 6
assert(n3 == N(4836))

n4 = N(0)
n3 >> n4; assert(n4.val == 6)
n3 >> n4; assert(n4.val == 3)
n3 >> n4; assert(n4.val == 8)
n3 >> n4; assert(n4.val == 4)

n5 = N(3)
assert(!n5 == 3)
assert(!n5 == 2)
assert(!n5 == 1)
